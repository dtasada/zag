import std;
import c;

import raylib as rl;

struct Player {
    position: rl.Vector2,
    color: rl.Color,

    pub fn init(x: f32, y: f32) Player {
        return Player {
            position: rl.Vector2 { x: x, y: y },
            color: rl.Color { r: 255, g: 0, b: 0, a: 255 },
        };
    }

    fn update(self: &mut Player) void {
        rl.DrawCircleV(self.position, 20.0, self.color);

        if (rl.IsKeyDown(cast<i32>(rl.KeyboardKey.W))) self.position.y -= 6;
        if (rl.IsKeyDown(cast<i32>(rl.KeyboardKey.A))) self.position.x -= 6;
        if (rl.IsKeyDown(cast<i32>(rl.KeyboardKey.S))) self.position.y += 6;
        if (rl.IsKeyDown(cast<i32>(rl.KeyboardKey.D))) self.position.x += 6;
    }
}

struct Zombie {
    position: rl.Vector2,
    color: rl.Color,

    pub fn init(x: f32, y: f32) Zombie {
        return Zombie {
            position: rl.Vector2.init(x, y),
            color: rl.Color.init(0, 255, 0, 255),
        };
    }

    fn update(self: &mut Zombie, player: &Player) void {
        let diff = player.position.subtract(self.position);
        self.position = self.position.add(diff.normalize().scale(2.0));

        rl.DrawCircleV(self.position, 20.0, self.color);
    }
}

fn add(x, y: i32) i32 -> x + y

fn main() void {
    rl.InitWindow(1280, 720, "Hello world!");
    defer rl.CloseWindow();

    c.printf("add: %d\n", add(4, 3));

    rl.SetTargetFPS(60);

    let mut player = Player.init(640, 360);

    let mut zombies = std.ArrayList<Zombie>.init();
    defer zombies.deinit();

    zombies.append(Zombie.init(0, 0));
    zombies.append(Zombie.init(0, 720));
    zombies.append(Zombie.init(1280, 0));
    zombies.append(Zombie.init(1280, 720));

    while (!rl.WindowShouldClose()) {
        rl.BeginDrawing();
        defer rl.EndDrawing();

        rl.ClearBackground(rl.Color { r: 0, g: 0, b: 0, a: 255 });

        for (zombies.items) |&mut zombie| zombie.update(&player);

        player.update();
    }
}
